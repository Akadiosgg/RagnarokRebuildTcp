#event "MilestoneEvent" //this disables this file if MilestoneEvent is not set in OperationConfig section of appsettings

//-----------------------------------
//Valkyrie NPC and Okolnir Entrance
//-----------------------------------

Npc("prt_fild08", "Valkyrie", "4_F_VALKYRIE", 133, 340, SE)
{
	local okolnir = 0;
	RegisterGlobalSignal("MilestoneValkyrie");
	HideNpc();
OnSignal:
	if(Signal == "RevealOnServerUp")
		ShowNpc();
	if(Signal == "Open")
		okolnir = 2;
	if(Signal == "Close")
	{
		okolnir = 3;
		SignalNpc("#okolnirentrance", "Close"); //close signal will come from global handler, but we'll send this signal locally
	}
	if(Signal == "ReOpen")
	{
		okolnir = 0;
	}
	if(Signal == "Reveal")
		StartTimer(1s);
OnTimer 20s:
	ShowNpc();
	PlayEffectAtMyLocation("PuffOfSmoke");
OnTimer 22s:
	ServerWideMessage("<color=#FF99FF>The Valkyrie has joined the camp south of Prontera!</color>", "", true);
	StopTimer();
OnClick:
	FocusNpc();
	string openOption = "";

	if(okolnir == 1)
	{
		Dialog(Name, "The door to okolnir will open shortly, please be patient. Once the portal is open all players will be able to enter freely until it is cleared.");
		return;
	}
	if(okolnir == 2)
	{
		Dialog(Name, "The door to okolnir is open. Good luck.");
	}
	if(okolnir == 3)
	{
		Dialog(Name, "Congratulations on clearing the okolnir challenge dungeon. There's a delay of about 5 minutes after a clear before you can try again.");
		return;
	}
	if(okolnir == 0)
	{
		if(GetItemCount("Okolnir_Key") >= 3)
		{
			Dialog(Name, "You have the three keys required to open the door to Okolnir. Are you interested in opening the portal?");
			openOption = "Open the Portal";
		}
		else
		{
			Dialog(Name, "Welcome adventurer. I can grant you access to the realm of Okolnir, if you can prove your worth.");
		}
	}

	Option(openOption, "Explain Okolnir", "Cancel")
	switch(result)
	{
		case 0:
			Dialog(Name, "The Okolnir challenge dungeon is not easy, and there is no limit to the number of participants. If you would prefer to wait until more players are available to enter that might be best.");
			Dialog(Name, "If you choose to proceed all online players will be notified, and the portal will open up here 5 minutes later. Anyone can enter, and the portal will stay open until the final objective is complete.");
			Option("Proceed", "Cancel");
			if(result == 0)
			{
				TakeItem("Okolnir_Key", 3);
				ServerWideMessage("<color=#FF99FF>The player " + PlayerName + " is opening the portal to Okolnir!</color>", "", true);
				SignalNpc("#okolnirentrance", "Open");
				okolnir = 1;
			}
		case 1:
			Dialog(Name, "Okolnir is a challenge event available for all players to participate in. One player must have 3 Okolnir keys to open the door.");
			Dialog(Name, "The door will remain open until the event is cleared, or the server goes offline. When opening the door there will be a 5 minute warning sent to all players so they can gather to participate.");
	}
}


Npc("prt_fild08", "#okolnirentrance", "WARP", 131, 338, SE, 1, 1)
{
	HideNpc();
OnSignal:
	if(Signal == "Open")
		StartTimer(1s);
	if(Signal == "Close")
		HideNpc();
	if(Signal == "OpenImmediately")
	{
		SendGlobalSignal("OkolnirReset", "Reset");
		ShowNpc();
		SignalNpc("Valkyrie", "Open");
		StopTimer();
	}
OnTimer 5s:
	ServerWideMessage("<color=#FF99FF>Okolnir is a challenge event all players can participate in. The portal will appear next to the Valkyrie south of prontera in 5 minutes.</color>", "", true);
OnTimer 50s:
	SendGlobalSignal("OkolnirReset", "Reset");
OnTimer 300s:
	ShowNpc();
	StopTimer();
	ServerWideMessage("<color=#FF99FF>The portal to Okolnir is now open!</color>", "", true);
	SignalNpc("Valkyrie", "Open");
OnTouch:
	MoveTo("que_qsch01", 58, 305);
}

Warp("que_qsch01", "firstexit", 58, 299, 1, 1, "prt_fild08", 160, 353);


//----------------------------
//challenge dungeon Okolnir
//----------------------------

Npc("que_qsch01", "#OkolnirRestart", "CLEAR_NPC", 5, 5, S)
{
	RegisterGlobalSignal("OkolnirReset");
OnSignal:
	SignalNpc("#Zone1Spawner", "Reset");
	SignalNpc("#Zone2Spawner", "Reset");
	SignalNpc("#Zone3Spawner", "Reset");
	SignalNpc("#Zone4Spawner", "Reset");
	SignalNpc("#Zone5Spawner", "Reset");
	SignalNpc("#warphome", "Open");
	MoveAllPlayersToMap("prt_fild08", 160, 353);
	ServerLog("Okolnir event is reset!");
}

Npc("que_qsch01", "#Zone1Spawner", "CLEAR_NPC", 68, 325, SE, 3, 7)
{
	HideFromView();
OnSignal:
	EnableTouchArea();
	KillMyMobs();
OnTouch:
	if(Random(0, 2) == 0) {
		SummonMobs(14, "BAPHOMET_", 107, 261, 21, 56);
		SummonMobs(8, "INCUBUS", 107, 261, 21, 56);
		SummonMobs(8, "SUCCUBUS", 107, 261, 21, 56);
		if(Random(0, 2) == 0)
		{
			SummonMobs(2, "DEVILING", 116, 240, 5, 5);
			SummonMobs(1, "GHOSTRING", 116, 240, 5, 5);
		}
		else
			SummonMobs(1, "ARCHANGELING", 116, 240, 5, 5);
	} else {
		SummonMobs(14, "NIGHTMARE", 107, 261, 21, 56);
		SummonMobs(8, "DEVIRUCHI", 107, 261, 21, 56);
		SummonMobs(4, "MARIONETTE", 107, 261, 21, 56);
		SummonMobs(6, "INCUBUS", 107, 261, 21, 56);
		SummonMobs(6, "SUCCUBUS", 107, 261, 21, 56);
		SummonMobs(1, "DOPPELGANGER", 116, 240, 5, 5);
	}
	DisableTouchArea();
	ServerLog("Okolnir trail spawner 1 activated.");
}

Npc("que_qsch01", "#Zone2Spawner", "CLEAR_NPC", 102, 223, SE, 4, 7)
{
	HideFromView();
OnSignal:
	EnableTouchArea();
	KillMyMobs();
OnTouch:
	if(Random(0, 2) == 0) {
		SummonMobs(15, "NINE_TAIL", 74, 151, 10, 64);
		SummonMobs(6, "AM_MUT", 74, 151, 10, 64);
		SummonMobs(6, "SKELETON_GENERAL", 74, 151, 10, 64);
		SummonMobs(1, "CAT_O_NINE_TAIL", 71, 163, 5, 5);
		SummonMobs(1, "MOONLIGHT", 72, 122, 5, 5);
	} else {
		SummonMobs(12, "ARGIOPE", 74, 151, 10, 64);
		SummonMobs(8, "ARGOS", 74, 151, 10, 64);
		SummonMobs(8, "PETIT", 74, 151, 10, 64);
		SummonMobs(8, "PETIT_", 74, 151, 10, 64);
		SummonMobs(12, "GIANT_HONET", 74, 151, 10, 64);
		SummonMobs(4, "DEATHSPORE", 74, 151, 10, 64);
		SummonMobs(1, "WERE_WOLF", 71, 163, 5, 5);
		SummonMobs(1, "MISTRESS", 72, 122, 5, 5);
	}
	DisableTouchArea();
	ServerLog("Okolnir trail spawner 2 activated.");
}

Npc("que_qsch01", "#Zone3Spawner", "CLEAR_NPC", 77, 108, SE, 8, 4)
{
	HideFromView();
OnSignal:
	EnableTouchArea();
	KillMyMobs();
OnTouch:
	if(Random(0, 2) == 0) {
		SummonMobs(12, "ANOLIAN", 123, 83, 39, 12);
		SummonMobs(2, "MOBSTER", 123, 83, 39, 12);
		SummonMobs(6, "MEDUSA", 123, 83, 39, 12);
		if(Random(1, 3) == 0) {
			SummonMobs(2, "MUTANT_DRAGON", 112, 79, 5, 5);
			SummonMobs(1, "GRYPHON", 126, 79, 5, 5);
		} else {
			SummonMobs(1, "MUTANT_DRAGON", 112, 79, 5, 5);
			SummonMobs(2, "GRYPHON", 126, 79, 5, 5);
		}		
		SummonMobs(6, "STALACTIC_GOLEM", 123, 83, 39, 12);
		SummonMobs(1, "TAO_GUNKA", 146, 79, 5, 5);
	} else {
		SummonMobs(12, "ISIS", 123, 83, 39, 12);
		SummonMobs(6, "ANUBIS", 123, 83, 39, 12);
		SummonMobs(1, "OSIRIS", 112, 79, 5, 5);
		if(Random(0, 2) == 0) {
			SummonMobs(10, "ANCIENT_MUMMY", 123, 83, 39, 12);
			SummonMobs(1, "AMON_RA", 146, 79, 5, 5);
		}
		else
		{
			SummonMobs(6, "MARDUK", 123, 83, 39, 12);
			SummonMobs(6, "PASANA", 123, 83, 39, 12);
			SummonMobs(1, "PHARAOH", 146, 79, 5, 5);
		}
	}
	DisableTouchArea();
	ServerLog("Okolnir trail spawner 3 activated.");
}

Npc("que_qsch01", "#Zone4Spawner", "CLEAR_NPC", 154, 81, SE, 2, 9)
{
	HideFromView();
OnSignal:
	EnableTouchArea();
	KillMyMobs();
OnTouch:
	var rnd = Random(0, 3);
	if(rnd == 0) {
		SummonMobs(4, "WANDER_MAN", 184, 132, 15, 41);
		SummonMobs(4, "KHALITZBURG", 184, 132, 15, 41);
		SummonMobs(10, "RAYDRIC", 184, 132, 15, 41);
		SummonMobs(1, "CHIMERA", 184, 132, 15, 41);
		SummonMobs(8, "RAYDRIC_ARCHER", 184, 132, 15, 41);
		SummonMobs(3, "KNIGHT_OF_ABYSS", 184, 153, 15, 19);
		if(Random(0, 2) == 0)
			SummonMobs(1, "BLOODY_KNIGHT", 184, 153, 15, 19);
		else {
			SummonMobs(1, "MYSTELTAINN", 184, 153, 15, 19);
			SummonMobs(1, "TIRFING", 184, 153, 15, 19);
			SummonMobs(1, "EXECUTIONER", 184, 153, 15, 19);
		}
		if(Random(0, 3) == 0)
			SummonMobs(1, "BAPHOMET", 184, 153, 15, 19);
		else
			SummonMobs(1, "CHIMERA", 184, 132, 15, 41);
	}
	if(rnd == 1) {
		SummonMobs(8, "RYBIO", 184, 132, 15, 41);
		SummonMobs(10, "INJUSTICE", 184, 132, 15, 41);
		SummonMobs(4, "PHENDARK", 184, 132, 15, 41);
		SummonMobs(3, "ZOMBIE_PRISONER", 184, 132, 15, 41);
		SummonMobs(3, "SKEL_PRISONER", 184, 132, 15, 41);
		SummonMobs(6, "GARGOYLE", 184, 132, 15, 41);
		SummonMobs(1, "ZHERLTHSH", 184, 153, 15, 19);
		if(Random(0, 3) == 0)
			SummonMobs(1, "DARK_LORD", 184, 153, 15, 19);
		else
		{
			SummonMobs(4, "ARCLOUSE", 184, 132, 15, 41);
			SummonMobs(8, "STING", 184, 153, 15, 19);
			SummonMobs(3, "MAJORUROS", 184, 153, 15, 19);
		}
	}
	if(rnd == 2) {
		SummonMobs(6, "DRAGON_TAIL", 182, 99, 10, 8);
		SummonMobs(4, "SPRING_RABBIT", 182, 99, 10, 8);
		SummonMobs(15, "PEST", 184, 132, 15, 41);
		SummonMobs(6, "PERMETER", 184, 132, 15, 41);
		SummonMobs(6, "FREEZER", 184, 132, 15, 41);
		SummonMobs(6, "HEATER", 184, 132, 15, 41);
		SummonMobs(6, "ASSULTER", 184, 132, 15, 41);
		SummonMobs(1, "TURTLE_GENERAL", 184, 153, 15, 19);
	}
	DisableTouchArea();

	ServerLog("Okolnir trail spawner 4 activated.");
}

Npc("que_qsch01", "#Zone5Spawner", "CLEAR_NPC", 174, 163, SE, 5, 9)
{
	HideFromView();
OnSignal:
	EnableTouchArea();
	KillMyMobs();
OnTouch:
	var rnd = Random(0, 3);
	if(rnd == 0) {
		SummonMobs(8, "SASQUATCH", 149, 170, 13, 6);
		SummonMobs(4, "LEIB_OLMAI", 149, 170, 13, 6);
		SummonMobs(2, "EDDGA", 142, 172, 1, 1);
	} 
	if(rnd == 1) {
		SummonMobs(18, "PENOMENA", 149, 170, 13, 6);
		SummonMobs(10, "MIMIC", 149, 170, 13, 6);
		SummonMobs(1, "DRAKE", 142, 172, 1, 1);
	}
	if(rnd == 2) {
		SummonMobs(12, "ORC_ARCHER", 149, 170, 13, 6);
		SummonMobs(12, "HIGH_ORC", 149, 170, 13, 6);
		SummonMobs(1, "ORC_LORD", 145, 172, 1, 1);
		SummonMobs(1, "ORK_HERO", 142, 172, 1, 1);
	}
	DisableTouchArea();
	ServerLog("Okolnir trail spawner 5 activated.");
	StartTimer(5s); //we do both timer and on kill event, you know, just in case
OnTimer 10s:
	if(MobCount == 0)
	{
        SignalNpc("#warptomid", "Reveal");
		StopTimer();
	}
	ResetTimer();
OnMobKill:
	ServerLog("Okolnir trail spawner 5 mob killed. Current active: " + MobCount);
    if(MobCount == 0)
	{
        SignalNpc("#warptomid", "Reveal");
		StopTimer();
	}
}

//Warp("que_qsch01", "warptomid", 132, 171, 1, 1, "que_qsch01", 114, 164);

Npc("que_qsch01", "#warptomid", "WARP", 132, 171, SE, 1, 1)
{
	HideNpc();
OnSignal:
	if(Signal == "Reveal")
	{
		ShowNpc();
		SignalNpc("#CenterAngelStage","Start");
		SignalNpc("#warphome", "Hide");
	}
	if(Signal == "Hide")
		HideNpc();
OnTouch:
	MoveTo("que_qsch01", 114, 164);
}

//%(132, 134, 16, 14)
Npc("que_qsch01", "#CenterAngelStage", "CLEAR_NPC", 5, 5, SE, 1, 1)
{
	HideFromView();
	local step = 0;
OnSignal:
	if(Signal == "Start")
	{
		SummonMobs(6, "OBSERVATION", 132, 134, 16, 14);
		SummonMobs(6, "RETRIBUTION", 132, 134, 16, 14);
		SummonMobs(6, "SOLACE", 132, 134, 16, 14);
		SummonMobs(6, "SHELTER", 132, 134, 16, 14);
		MoveLockAllMyMonsters(%(132, 136, 16, 16));
		ServerLog("Center area event has activated.");
		step = 1;
	}

	if(Signal == "NextStep" && step == 1)
	{
		ServerLog("Angels killed, starting valkyrie phase.");
		step = 2;
		return;
	}

	if(Signal == "Stop")
	{
		step = 0;
		KillMyMobs();
		StopTimer();
	}

	StartTimer(5s); //we do both timer and on kill event, you know, just in case
OnTimer 20s:
	if(MobCount == 0)
	{
        SignalNpc("#CenterStageValkyrie", "Start");
		StopTimer();
	}
	ResetTimer();
OnMobKill:
	ServerLog("Center area mob killed. Current active: " + MobCount);
    if(MobCount == 0)
	{
        SignalNpc("#CenterStageValkyrie", "Start");
		StopTimer();
	}
}

Npc("que_qsch01", "#CenterStageValkyrie", "CLEAR_NPC", 5, 5, SE, 1, 1)
{
	HideFromView();
	local step = 0;
OnSignal:
	if(Signal == "Start")
	{
		DescendSummonMonster("VALKYRIE", 125, 142, 0, 0, NW, "EVENT_VALKYRIE");
		DescendSummonMonster("VALKYRIE", 138, 142, 0, 0, NE, "EVENT_VALKYRIE");
		DescendSummonMonster("VALKYRIE", 125, 129, 0, 0, SW, "EVENT_VALKYRIE");
		DescendSummonMonster("VALKYRIE", 138, 129, 0, 0, SE, "EVENT_VALKYRIE");
		MoveLockAllMyMonsters(%(132, 136, 16, 16));
		ServerLog("Center area event has activated.");
		step = 1;
	}

	if(Signal == "Stop")
	{
		step = 0;
		KillMyMobs();
		StopTimer();
	}

	if(Signal == "NextStep" && step == 1)
	{
		ServerLog("Valkyrie's killed, starting randgris phase.");
		step = 2;
		return;
	}

	StartTimer(5s); //we do both timer and on kill event, you know, just in case
OnTimer 20s:
	if(MobCount == 0)
	{
        SignalNpc("#FinalValkyrieStage", "Start");
		StopTimer();
	}
	ResetTimer();
OnMobKill:
	ServerLog("Center area stage 2 mob killed. Current active: " + MobCount);
    if(MobCount == 0)
	{
        SignalNpc("#FinalValkyrieStage", "Start");
		StopTimer();
	}
}

Npc("que_qsch01", "#FinalValkyrieStage", "CLEAR_NPC", 132, 135, SE, 1, 1)
{
	HideFromView();
OnSignal:
	if(Signal == "Start")
	{
		RegisterMapWideEffect("OkolnirUnsafeZone");
		MapWideMessage("The area around the center platform has become unsafe!");
		StartTimer(1s);
	}
	
	if(Signal == "End")
	{
		UnregisterImportant();
		KillMyMobs();
		EndAllEvents();
	}

	if(Signal == "Fail")
	{
		ServerLog("A full wipe of the randgris phase has occured.");
	}
OnTimer 2s:
	CreateEvent("OkolnirDamageZone", 5, 5, "", false);
OnTimer 8s:
	//activate dangerzone
	MapWideMessage("Valkyrie Randgris joins the battle!");
OnTimer 10s:
	DescendSummonMonster("RANDGRIS", 132, 135, 0, 0, S, "EVENT_RANDGRIS");
	MoveLockAllMyMonsters(%(132, 136, 16, 16));
	StopTimer();
OnMobKill:
	ServerLog("Center final stage mob killed. Current active: " + MobCount);
    if(MobCount == 0)
	{
        //SignalNpc("#CenterAngelStage", "NextStep");
		StopTimer();
		EndAllEvents();
		UnregisterImportant();
		SignalNpc("#warphome", "Open");
		SignalNpc("#ReopenTimer", "Start");
		SendGlobalSignal("MilestoneValkyrie", "Close");
		if(GetGlobalInt("OkolnirFirstClear") > 1)
			MapWideMessage("The Okolnir event has been cleared!");
	}
}

Npc("que_qsch01", "#warphome", "WARP", 149, 105, SE, 1, 1)
{
OnSignal:
	if(Signal == "Open")
		ShowNpc();
	else
		HideNpc();
OnTouch:
	MoveTo("prt_fild08", 137, 333);
}

Npc("que_qsch01", "#VictorySpeachHandler", "CLEAR_NPC", 5, 5, SE)
{
	HideFromView();
	RegisterGlobalSignal("OkolnirFirstClear");
OnSignal:
	StartTimer();
//OnTimer 5s:
	//ServerWideMessage("<color=#FF99FF>The Okolnir challenge dungeon has been cleared!</color>", "", true);
OnTimer 15s:
	ServerWideMessage("<color=#FF99FF>A thanks to all participants of the Rangarok Rebuild test, I hope everyone has enjoyed their time here.</color>", "", true);
OnTimer 28s:
	ServerWideMessage("<color=#FF99FF>The Okolnir event represents the final challenge prepared for the test, but the server will continue until the end of the test duration. The event can be re-started by talking to the Valkyrie with 3 keys.</color>", "", true);
OnTimer 42s:
	ServerWideMessage("<color=#FF99FF>I'd love to hear your feedback about what you like and disliked about the test. Thanks again for joining and giving this a try!</color>", "", true);
	SetGlobalInt("OkolnirFirstClear", 2);
	StopTimer();
}

Npc("que_qsch01", "#ReopenTimer", "CLEAR_NPC", 5, 5, SE)
{
OnSignal:
	StartTimer();
OnTimer 300s:
	StopTimer();
	SendGlobalSignal("MilestoneValkyrie", "ReOpen");
}

Npc("que_qsch01", "#RandgrisHelper", "CLEAR_NPC", 5, 6, SE)
{
OnSignal:
	if(Signal == "Spawn")
	{
		DescendSummonMonster(Src, "VALKYRIE", 127, 136, 0, 0, W, "EVENT_VALKYRIE");
		DescendSummonMonster(Src, "VALKYRIE", 136, 136, 0, 0, E, "EVENT_VALKYRIE");
	}
	if(Signal == "Die")
	{
		KillMyMobs();
	}
}

Npc("que_qsch01", "#RandgrisHelper2", "CLEAR_NPC", 5, 6, SE)
{
OnSignal:
	if(Signal == "Spawn")
	{
		DescendSummonMonster("VALKYRIE", 125, 142, 0, 0, NW, "EVENT_VALKYRIE");
		DescendSummonMonster("VALKYRIE", 138, 142, 0, 0, NE, "EVENT_VALKYRIE");
		DescendSummonMonster("VALKYRIE", 125, 129, 0, 0, SW, "EVENT_VALKYRIE");
		DescendSummonMonster("VALKYRIE", 138, 129, 0, 0, SE, "EVENT_VALKYRIE");
		MoveLockAllMyMonsters(%(132, 136, 16, 16));
	}
	if(Signal == "Die")
	{
		KillMyMobs();
	}
}

event OkolnirLootEvent {
	StartTimer(100);
OnTimer 1000:
	EndEvent();
}

//----------------------------
// Combat AI related
//----------------------------

//caster clone AI
AltSkillHandler("EVENT_VALKYRIE") {
	OnInit:
		FlagAsMvp();
		Cast(NoCast, 10, 5s, 5s, EasyInterrupt | HideCastBar | HideSkillName);
	StateIdle:
		TryCast(Heal, 9999, 100%, 0s, 5s)[HpPercent < 60];
	StateChase:
		TryCast(Heal, 9999, 100%, 0.7s, 5s)[FindAllyBelowHpPercent(60)];
	StateAttacking:
		TryCast(Heal, 9999, 100%, 0.7s, 5s)[FindAllyBelowHpPercent(60)];
		TryCast(SonicBlow, 1, 5%, 1s, 10s);
		TryCast(JupitelThunder, 3, 2%, 2s, 10s);
}

AltSkillHandler("EVENT_RANDGRIS") {
	OnInit:
		FlagAsMvp();
		Cast(NoCast, 10, 2s, 5s, EasyInterrupt | HideCastBar | HideSkillName); //force her to not aggro for the first 2s on spawn in
	StateIdle:
		TryCast(Heal, 9999, 100%, 0s, 3s)[HpPercent < 50];
	StateChase:
		TryCast(CallMinion, 1, 100%, 1.5s, 0s, NoInterrupt)[HpPercent < 85 && IsNamedEventOffCooldown("SummonMinions1")] -> {
			SignalNpc("#RandgrisHelper", "Spawn");
			SetEventCooldown("SummonMinions1", -1);
		}
		TryCast(CallMinion, 1, 100%, 1.5s, 0s, NoInterrupt)[HpPercent < 45 && IsNamedEventOffCooldown("SummonMinions2")] -> {
			SignalNpc("#RandgrisHelper2", "Spawn");
			SetEventCooldown("SummonMinions2", -1);
		}
		TryCast(EarthShaker, 1, 20%, 4s, 20s, NeverInterrupt | EventOnStartCast)[HpPercent < 95]  -> {
			CreateEventOnTarget("EarthShakerCastEvent", 2 + (100 - HpPercent) / 33);
			if(HpPercent < 50)
				SetStateFlag(0, 1);
		}
		TryCast(NoCast, 1, 100%, 0, 0)[GetStateFlag(0) > 0] -> {
			CreateEventOnTarget("EarthShakerCastEvent", 2 + (100 - HpPercent) / 33);
			SetStateFlag(0, 0);
		}
		TryCast(RollingThunder, 1, 20%, 2.2s, 25s, NeverInterrupt | EventOnStartCast)[HpPercent < 90]  -> CreateEventOnTarget("ExaflareControlEvent", 2 + (100 - HpPercent) / 33);
		TryCast(LordOfVermilion, 11, 10%, 2.8s, 25s, RandomTarget);
		TryCast(JupitelThunder, 9, 5%, 0.7s, 15s);
	StateAttacking:
		TryCast(CallMinion, 1, 100%, 1.5s, 0s, NoInterrupt)[HpPercent < 80 && IsNamedEventOffCooldown("SummonMinions1")] -> {
			SignalNpc("#RandgrisHelper", "Spawn");
			SetEventCooldown("SummonMinions1", -1);
		}
		TryCast(CallMinion, 1, 100%, 1.5s, 0s, NoInterrupt)[HpPercent < 35 && IsNamedEventOffCooldown("SummonMinions2")] -> {
			SignalNpc("#RandgrisHelper2", "Spawn");
			SetEventCooldown("SummonMinions2", -1);
		}
		TryCast(EarthShaker, 1, 20%, 4s, 20s, NeverInterrupt | EventOnStartCast)[HpPercent < 95]  -> {
			CreateEventOnTarget("EarthShakerCastEvent", 2 + (100 - HpPercent) / 33);
			if(HpPercent < 50)
				SetStateFlag(0, 1);
		}
		TryCast(NoCast, 1, 100%, 0, 0)[GetStateFlag(0) > 0] -> {
			CreateEventOnTarget("EarthShakerCastEvent", 2 + (100 - HpPercent) / 33);
			SetStateFlag(0, 0);
		}
		TryCast(RollingThunder, 1, 20%, 2.2s, 25s, NeverInterrupt | EventOnStartCast)[HpPercent < 90]  -> CreateEventOnTarget("ExaflareControlEvent", 2 + (100 - HpPercent) / 33);
		TryCast(LordOfVermilion, 11, 5%, 2.8s, 25s, RandomTarget);
		TryCast(SonicBlow, 1, 5%, 1s, 10s);
		TryCast(JupitelThunder, 9, 5%, 0.7s, 15s);
		TryCast(TwoHandQuicken, 1, 100%, 0, 120s)[HpPercent < 25];
		TryCast(PowerUp, 2, 100%, 0.7s, 25s)[HpPercent < 25];
	OnDie:
		CallSpecialMonsterEvent("OkolnirEnd");
		SignalNpc("#RandgrisHelper", "Die");
		SignalNpc("#RandgrisHelper2", "Die");
}
