
//--------------------------------------------------------------
// Turtle Island
//--------------------------------------------------------------

Npc("alberta", "Sailor", "4_M_SEAMAN", 247, 122, S)
{
    RegisterAsWarpNpc();
    localstr name = "Gotanblue";
OnClick:
    FocusNpc();
    Dialog(name, "Heh... I can see it in your eyes...\nI can tell you're curious about Turtle Island. I'm not sure where you heard about it, but I can help you out.");
    Option("Turtle Island -> 10000z", "Cancel" );
    switch(Result)
    {
        case 0:
            if(GetZeny() < 10000)
            {
                Dialog(name, "Hmm... Sorry, but you don't have enough zeny. I hope you understand I can't let my expertise and experience be undervalued...");
                return;
            }
            Dialog(name, "Alright!!\nYou've made your choice! With my experience, we'll arrive without fail! I appreciate your spirit!\n<color=#3355FF>- choo-choo -</color>");
            DropZeny(10000);
            MoveTo("tur_dun01", 157, 39);
            return;
        case 1:
            Dialog(name, "Alright then...\nWell, should the spirit of adventure grab you, I'll be here waiting.");
    }
}

Npc("tur_dun01", "Sailor", "4_M_SEAMAN", 165, 29, S)
{
    RegisterAsWarpNpc();
    localstr name = "Gotanblue";
OnClick:
    FocusNpc();
    Dialog(name, "Do you want to return to Alberta?");
    Option("Go to Alberta", "Not yet" );
    switch(Result)
    {
        case 0: 
            Dialog(name, "Heh heh... I hope you've found what you were looking for. Alright, even an adventurer needs to return home every now and then.\n<color=#3355FF>- choo-choo -</color>");
            MoveTo("alberta", 241, 115);
    }
}

//--------------------------------------------------------------
// Amatsu
//--------------------------------------------------------------

Npc("alberta", "Overseas Captain", "4_M_SEAMAN", 245, 93, SW) 
{
    RegisterAsWarpNpc();
    localstr name = "Sea Captain";
OnClick:
    FocusNpc();
    Dialog(name, "Hey, there. You've heard of Amatsu, the land beyond the sea? I wasn't planning on making the voyage any time soon, but I could get you there if you're willing to pay.");
    Option("Travel to Amatsu -> 10000z", "Cancel");
    switch(Result)
    {
        case 0:
            if(GetZeny() < 10000)
            {
                Dialog(name, "Were you even listening? 10,000 zeny. You're an adventurer right? Go loot some dungeon or shipwreck or something and come back when you got the money.");
                return;
            }
            Dialog(name, "Alright, I'll start the engine!");
            DropZeny(10000);
            MoveTo("amatsu", 197, 83);
    }
}

//--------------------------------------------------------------
// Sunken Ship
//--------------------------------------------------------------

Npc("alberta", "Fisk", "4W_SAILOR", 189, 151, SE) { 
OnClick:
    FocusNpc();
    Dialog(Name, "Ahoy mate, where'd ya wanna go?");
    Option("Sunken Ship -> 250 zeny.", "Izlude Marina -> 500 zeny.", "Never mind.");
    switch(Result)
    {
        case 0:
            if(GetZeny() < 250)
            {
                Dialog(Name, "Hey now, don't try to cheat me! I said 250 zeny!");
                return;
            }
            DropZeny(250);
            MoveTo("alb2trea", 43, 53);
        case 1:
            if(GetZeny() < 500)
            {
                Dialog(Name, "Ain't no way yer getting there without the 500 zeny first!");
                return;
            }
            DropZeny(500);
            MoveTo("izlude", 176, 182);
        case 2:
            Dialog(Name, "Alright... Landlubber.");
    }
}

Npc("alb2trea", "Fisk", "4W_SAILOR", 39, 50, E) { 
OnClick:
    FocusNpc();
    Dialog(Name, "So you wanna head back to the mainland in Alberta, eh?");
    Option("Yes please.", "I changed my mind." );
    switch(Result)
    {
        case 0: MoveTo("alberta", 192, 169);
    }
}

//--------------------------------------------------------------
// Refining
//--------------------------------------------------------------

Npc("alberta_in", "Fredrik", "4_M_03", 28, 58, N)
{
OnClick:
    FocusNpc();
	Dialog(Name, "I'm " + Name + ", the Armsmith.\nI can refine all kinds of weapons, armor and equipment, so let me know if you want me to refine something.");
	Option("Refine Gear", "Cancel");
	if(result > 0)
		return;

	OpenRefineDialog();
}


Npc("alberta_in", "Xenophon", "4_M_02", 21, 63, SE) {
	AddTradedItem(CreateTradedItem("Oridecon").Requires("Rough_Oridecon",5));
	AddTradedItem(CreateTradedItem("Elunium").Requires("Rough_Elunium",5));
OnClick:
	FocusNpc();
	if(InventoryItemCount > MaxItemCount - 1) {
		Dialog(Name, "Whoa, you sure are carrying a lot! Come back when you have a few less items on you.");
		return;
	}
	Dialog(Name, "I can purify your Rough Oridecon or Rough Elunium stones. I'll need 5 Rough stones to make 1 pure one for you.");
	Option("Trade", "Cancel");
	if(result == 0) StartItemTrade();
}

Npc("alberta_in", "Kahlamanlith", "4_M_04", 13, 71, SW) {
	SellItem("Phracon");
	SellItem("Emveretarcon");
OnClick:
	FocusNpc();
	if(HasJobType("Merchant"))
		Dialog(Name, "I sell 2 kinds of Metal for tempering weaponry.\nI have <color=#007777>Phracon<color=#000000> for Level 1 Weapons," +
					" and <color=#007777>Emveretarcon<color=#000000> for Level 2 Weapons. No discounts though, sorry.");
	else
		Dialog(Name, "I sell 2 kinds of Metal for tempering weaponry.\nI have <color=#007777>Phracon<color=#000000> for Level 1 Weapons," +
		            " and <color=#007777>Emveretarcon<color=#000000> for Level 2 Weapons.");
	
	Option("Buy ores", "Cancel");
	if(result == 0)
		OpenShop(false); //false makes the shop not apply discount
}

Npc("alberta_in", "Repair Man", "4_M_04", 31, 65, S)
{
OnClick:
    FocusNpc();
	Dialog(Name, "Normally I'd help you out if you needed your gear repaired, but for some reason nothing ever breaks! You guys sure do have it lucky.");
}